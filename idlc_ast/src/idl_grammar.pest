COMMENT_VARIANT_DOUBLESLASH = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
COMMENT_VARIANT_ASTERISK    = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
DOCUMENTATION               = @{ "/**" ~ NEWLINE ~ (!"*/" ~ ANY)* ~ "*/" }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT    = ${ DOCUMENTATION | COMMENT_VARIANT_ASTERISK | COMMENT_VARIANT_DOUBLESLASH }

path    = @{ (!".idl" ~ ANY)+ ~ ".idl" }
include = ${ "include" ~ " "+ ~ "\"" ~ path ~ "\"" }

ident          = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
integer_type   = @{ "u"? ~ "int" ~ ("8" | "16" | "32" | "64") }
float_type     = @{ "float" ~ ("32" | "64") }
primitive_type = @{ (integer_type | float_type) }
value          = @{ ("0x" ~ ASCII_HEX_DIGIT+ | "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?) }

array        = @{ ASCII_DIGIT+ }
struct_field =  { (primitive_type | ident) ~ ("[" ~ array ~ "]")? ~ ident ~ ";" }
struct       =  { "struct " ~ ident ~ "{" ~ (struct_field)+ ~ "};" }

const = { "const " ~ primitive_type ~ ident ~ "=" ~ value ~ ";" }

param_arr  = @{ "[]" }
param_type = ${ (primitive_type | ident | "interface") ~ param_arr? | "buffer" }
mutability = @{ ("in" | "out") }
param      =  { mutability ~ param_type ~ ident }
function   =  { "method " ~ ident ~ "(" ~ param? ~ ("," ~ param)* ~ ");" }

error     = { "error " ~ ident ~ ";" }
iname     = { ident ~ (":" ~ ident)? }
interface = { "interface " ~ iname ~ "{" ~ (function | const | error)* ~ "};" }

idl = { SOI ~ include* ~ (const | struct | interface)* ~ EOI }
